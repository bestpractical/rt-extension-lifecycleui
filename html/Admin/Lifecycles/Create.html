<& /Admin/Elements/Header, Title => loc("Admin Lifecycles") &>
<& /Elements/Tabs &>

<form action="<%RT->Config->Get('WebPath')%>/Admin/Lifecycles/Create.html" name="CreateLifecycle" method="post" enctype="multipart/form-data">

<&|/l&>New lifecycle name</&>: <input name="Name" value="" />
<hr />

% for my $type (@types) {
% my @lifecycles = @{ $lifecycles{$type} };
<h2><&|/l, $type&>"[_1]" lifecycles</&></h2>
% for my $lifecycle (@lifecycles) {
<button type="submit" name="Clone" value="<% $type %>--<% $lifecycle %>"><&|/l, $lifecycle&>Clone "[_1]" lifecycle</&></button><br>
% }
<br>
<button type="submit" name="Blank" value="<% $type %>"><&|/l, $type&>Blank "[_1]" lifecycle</&></button><br>
% }
</form>

<%INIT>
my @types = List::MoreUtils::uniq(
    'ticket',
    'asset',
    sort keys %RT::Lifecycle::LIFECYCLES_TYPES,
);

my %lifecycles;

for my $type (@types) {
    @{ $lifecycles{$type} } = sort { loc($a) cmp loc($b) }
                              grep { $_ ne 'approvals' }
                              RT::Lifecycle->ListAll($type);
}

if (defined($Clone) || defined($Blank)) {
    my ($type, $clone_of);

    if (defined($Clone)) {
        ($type, $clone_of) = split '--', $Clone;
    }
    else {
        $type = $Blank;
    }

    my ($ok, $msg) = RT::Extension::LifecycleUI->CreateLifecycle(
        CurrentUser => $session{CurrentUser},
        Name        => $Name,
        Type        => $type,
        Clone       => $clone_of,
    );

    Abort($msg) if !$ok;

    my $type_uri = $type;
    my $Name_uri = $Name;
    RT::Interface::Web::EscapeURI(\$type_uri);
    RT::Interface::Web::EscapeURI(\$Name_uri);

    RT::Interface::Web::Redirect( RT->Config->Get('WebURL') ."Admin/Lifecycles/Modify.html?Type=". $type_uri."&Name=".$Name_uri);
}
</%INIT>
<%ARGS>
$Clone => undef
$Blank => undef
$Name => undef
</%ARGS>
